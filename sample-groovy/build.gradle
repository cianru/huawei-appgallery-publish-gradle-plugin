import ru.cian.Dependencies

buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
    }

    dependencies {
        classpath "ru.cian:huawei-publish-gradle-plugin:" + Dependencies.sample.huaweiPlugin
    }
}

apply plugin: "com.android.application"
apply plugin: "ru.cian.huawei-publish"

huaweiPublish {
    instances {
        demoRelease {
            credentialsPath = "$projectDir/huawei-credentials-1.json"
            deployType = "upload-only"
            buildFormat = "apk"
        }
        fullRelease {
            credentialsPath = "$projectDir/huawei-credentials-2.json"
            deployType = "publish"
            buildFormat = "aab"
            publishTimeoutMs = 15_000
            publishPeriodMs = 3_000
            releaseTime = "2025-10-21T06:00:00+0300"
            releasePhase = new ru.cian.huawei.publish.ReleasePhaseExtension(
                "2021-10-18T21:00:00+0300",
                "2025-10-21T06:00:00+0300",
                1.0
            )
            releaseNotes = [
                new ru.cian.huawei.publish.ReleaseNote(
                    "ru-RU",
                    "$projectDir/release-notes-ru.txt"
                ),
                new ru.cian.huawei.publish.ReleaseNote(
                    "en-EN",
                    "$projectDir/release-notes-en.txt"
                ),
            ]
            appBasicInfo = "$projectDir/app-basic-info.json"
        }
    }
}

android {
    compileSdkVersion Dependencies.android.compileSdkVersion
    buildToolsVersion Dependencies.android.buildToolsVersion

    defaultConfig {
        applicationId "ru.cian.huawei.sample_groovy"
        minSdkVersion Dependencies.android.minSdkVersion
        targetSdkVersion Dependencies.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        full {
            dimension "version"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation Dependencies.libs.appcompat
    implementation Dependencies.libs.kotlinStdlib
}
