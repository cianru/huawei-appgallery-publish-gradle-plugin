buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:" + libs.versions.kotlin.get()
        classpath "ru.cian:huawei-publish-gradle-plugin:" + libs.versions.sampleHuaweiPlugin.get()
    }
}

apply plugin: "com.android.application"
apply plugin: "ru.cian.huawei-publish"

huaweiPublish {
    instances {
        demoRelease {
            credentialsPath = "$projectDir/huawei-credentials-1.json"
            deployType = "upload-only"
            buildFormat = "apk"
        }
        fullRelease {
            credentialsPath = "$projectDir/huawei-credentials-2.json"
            deployType = "publish"
            buildFormat = "aab"
            publishTimeoutMs = 15_000
            publishPeriodMs = 3_000
            releaseTime = "2025-10-21T06:00:00+0300"
            releasePhase = new ru.cian.huawei.publish.ReleasePhaseExtension(
                "2021-10-18T21:00:00+0300",
                "2025-10-21T06:00:00+0300",
                1.0
            )
            releaseNotes = new ru.cian.huawei.publish.ReleaseNotesExtension(
                [
                    new ru.cian.huawei.publish.ReleaseNote(
                        "ru-RU",
                        "$projectDir/release-notes-ru.txt"
                    ),
                    new ru.cian.huawei.publish.ReleaseNote(
                        "en-EN",
                        "$projectDir/release-notes-en.txt"
                    ),
                ],
                true
            )
            appBasicInfo = "$projectDir/app-basic-info.json"
        }
    }
}

android {
    compileSdk libs.versions.compileSdkVersion.get() as Integer

    namespace 'ru.cian.huawei.sample.groovy'

    defaultConfig {
        applicationId "ru.cian.huawei.sample.groovy"
        minSdkVersion libs.versions.minSdkVersion.get() as Integer
        targetSdkVersion libs.versions.targetSdkVersion.get() as Integer
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        full {
            dimension "version"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation(platform(libs.kotlinBom))
}
